# Copyright 2016 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

declare_args() {
  # if true, lwiperf server will run
  lwiperf = false

  # if true, use netstack2 for netstack
  netstack2 = false
}

group("netstack") {
  deps = [
    ":app",
    "//apps/netstack/apps:netstack_debug",
  ]
  if (netstack2) {
    deps += [ ":netstack2" ]
  }
}

executable("app") {
  if (netstack2) {
    output_name = "netstack1"
  } else {
    output_name = "netstack"
  }

  sources = [
    "dispatcher.c",
    "dispatcher.h",
    "handle_watcher.c",
    "handle_watcher.h",
    "iostate.c",
    "iostate.h",
    "main.c",
    "multiplexer.h",
    "net_init.h",
    "net_socket.h",
    "request_queue.c",
    "request_queue.h",
    "socket_functions.c",
    "socket_functions.h",
    "trace.c",
    "trace.h",
  ]

  sources += [
    "ports/lwip/eth-client.c",
    "ports/lwip/eth-client.h",
    "ports/lwip/ethernetif.c",
    "ports/lwip/ethernetif.h",
    "ports/lwip/lwip-netdb.h",
    "ports/lwip/lwip-socket.h",
    "ports/lwip/multiplexer-lwip.c",
    "ports/lwip/net_init-lwip.c",
    "ports/lwip/net_socket-lwip.c",
  ]

  defines = []

  if (is_debug) {
    defines += [ "NETSTACK_DEBUG" ]
  }

  if (lwiperf) {
    defines += [ "USE_LWIPERF" ]
  }

  libs = [
    "mxio",
    "magenta",
  ]

  deps = [
    "//third_party/lwip",
  ]
}

action("netstack2") {
  if (netstack2) {
    out_path = "$root_out_dir/netstack"
  } else {
    out_path = "$root_out_dir/netstack2"
  }

  outputs = [
    out_path,
  ]

  script = rebase_path("//.") + "/build/gobuild.py"
  depfile_path = "${out_path}.d"
  depfile = depfile_path
  console = true # only run one gobuild.py at once

  args = [
    "--fuchsia-root",
    rebase_path("//."),
    "--root-out-dir",
    rebase_path("$root_out_dir"),
    "--depfile",
    rebase_path(depfile_path),
    "--current-cpu",
    current_cpu,
  ]

  if (netstack2) {
    args += [
      "--binname",
      "netstack",
    ]
  }

  args += [ "apps/netstack/netstack2" ]

  deps = [
    "//third_party/go:go_runtime",
  ]
}
