# Copyright 2016 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

group("netstack") {
  deps = [
    ":app",
    "//apps/netstack/apps:netstack_debug",
  ]
}

executable("app") {
  output_name = "netstack"

  sources = [
    "dispatcher.c",
    "dispatcher.h",
    "handle_watcher.c",
    "handle_watcher.h",
    "iostate.c",
    "iostate.h",
    "main.c",
    "multiplexer.h",
    "net_init.h",
    "net_socket.h",
    "request_queue.c",
    "request_queue.h",
    "socket_functions.c",
    "socket_functions.h",
    "trace.c",
    "trace.h",
  ]

  sources += [
    "ports/lwip/multiplexer-lwip.c",
    "ports/lwip/net_init-lwip.c",
    "ports/lwip/net_socket-lwip.c",
    "ports/lwip/lwip-socket.h",
    "ports/lwip/lwip-netdb.h",
  ]

  if (is_debug) {
    defines = [ "NETSTACK_DEBUG" ]
  }

  libs = [
    "mxio",
    "magenta",
  ]

  deps = [
    "//third_party/lwip",
  ]
}

action("netstack2") {
  out_path = "$root_out_dir/netstack2"

  outputs = [ out_path ]

  script = rebase_path("//.") + "/build/gobuild.py"
  depfile_path = "${out_path}.d"
  depfile = depfile_path

  args = [
    "--fuchsia-root",
    rebase_path("//."),
    "--root-out-dir",
    rebase_path("$root_out_dir"),
    "--depfile",
    rebase_path(depfile_path),
    "--current-cpu",
    current_cpu,
    "apps/netstack/netstack2",
  ]

  deps = [
    "//third_party/go:go_runtime",
  ]
}
